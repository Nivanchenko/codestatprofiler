#Использовать logos

Перем СтрокиФайлов;
Перем Лог;

&Желудь
Процедура ПриСозданииОбъекта()

	Очистить();
	Лог = Логирование.ПолучитьЛог("oscript.lib.codestatprofiler.КешФайлов");

КонецПроцедуры

Процедура Очистить() Экспорт
	СтрокиФайлов = Новый Соответствие(); 
КонецПроцедуры


Функция ПолучитьСтрокуИзФайла(ИмяФайла, НомерСтроки) Экспорт
	Попытка
		СтрокиФайла = ПолучитьФайл(ИмяФайла);
		Строка = СтрокиФайла[Число(НомерСтроки) - 1];	
	Исключение
		ТекстОшибки = СтрШаблон("Ошибка чтение строки %1 файла %2 
								| %3", НомерСтроки, ИмяФайла, ОписаниеОшибки());
		Лог.Ошибка(ТекстОшибки);
		Строка = "";
	КонецПопытки;
	
	Возврат Строка;
КонецФункции

Функция ПолучитьФайл(ИмяФайла)
	Строки = СтрокиФайлов[ИмяФайла];
	Если Строки = Неопределено Тогда
		Строки = ПрочитатьФайл(ИмяФайла);
	КонецЕсли;

	Возврат Строки;
КонецФункции

Функция ПрочитатьФайл(ИмяФайла)

	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8);
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	МассивСтрок = СтрРазделить(Текст, Символы.ПС, Истина);

	СтрокиФайлов.Вставить(ИмяФайла, МассивСтрок);

	Возврат МассивСтрок;
	
КонецФункции